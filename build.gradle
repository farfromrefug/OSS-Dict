plugins {
    id 'com.android.application' version '8.13.2'
}

repositories {
    mavenCentral()
    google()
    maven {
        url "https://jitpack.io"
    }
}

android {
    compileSdkVersion 36

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    defaultConfig {
        namespace "itkach.aard2"
        applicationId "com.akylas.aard2"
        minSdk 21
        targetSdk 36
        versionCode 71
        versionName "1.1.3"
    }


    packagingOptions {
        project("slobj").file("exclude.txt").eachLine {
            line ->
                println line
                if (line && line.trim() && !line.startsWith("#")) {
                    exclude line.trim()
                }
        }
        resources {
            excludes += ['META-INF/*.version', 'META-INF/LICENSE', 'META-INF/NOTICE']
        }
    }


    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }
    }

    if (System.getenv("KEYSTORE") != null) {
        signingConfigs {
            release {
                storeFile file(System.getenv("KEYSTORE"))
                storePassword System.getenv("STORE_PASSWORD")
                keyPassword System.getenv("KEY_PASSWORD")
                if (keyPassword == null) {
                    keyPassword = storePassword
                }
                keyAlias "aard2-android"
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            if (System.getenv("KEYSTORE") != null) {
                signingConfig signingConfigs.release
            }
        }
    }
    buildFeatures {
        buildConfig true
    }
    dependenciesInfo {
        // Disables dependency metadata when building APKs.
        includeInApk = false
        // Disables dependency metadata when building Android App Bundles.
        includeInBundle = false
    }
    lint {
        abortOnError false
    }
}

dependencies {
    api project(':slobj')

    implementation "com.google.android.material:material:1.13.0"
    implementation "androidx.appcompat:appcompat:1.7.1"

    implementation "androidx.viewpager2:viewpager2:1.1.0"
    implementation 'androidx.documentfile:documentfile:1.1.0'

    // Fix duplicate classes issue in material
    configurations {
        configureEach {
            exclude group: 'androidx.lifecycle', module: 'lifecycle-viewmodel-ktx'
        }
    }
    implementation "androidx.webkit:webkit:1.15.0"

    api 'com.fasterxml.jackson.core:jackson-databind:2.20.1'
    api 'com.fasterxml.jackson.core:jackson-core:2.20.1'
    api 'com.fasterxml.jackson.core:jackson-annotations:2.20'
}
