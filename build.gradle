plugins {
    id 'com.android.application' version '7.3.0'
}

repositories {
    mavenCentral()
    google()
    maven {
        url "https://jitpack.io"
    }
}

android {
    namespace "itkach.aard2"
    compileSdkVersion 34

    packagingOptions {
        project("slobj").file("exclude.txt").eachLine {
            line ->
                println line
                if (line && line.trim() && !line.startsWith("#")) {
                    exclude line.trim()
                }
        }
        resources {
            excludes += ['META-INF/LICENSE', 'META-INF/NOTICE']
        }
    }


    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }
    }

    if (System.getenv("KEYSTORE") != null) {
        signingConfigs {
            release {
                storeFile file(System.getenv("KEYSTORE"))
                storePassword System.getenv("STORE_PASSWORD")
                keyPassword System.getenv("KEY_PASSWORD")
                if (keyPassword == null) {
                    keyPassword = storePassword
                }
                keyAlias "aard2-android"
            }
        }
    }

    buildTypes {
        if (System.getenv("KEYSTORE") != null) {
            release {
                signingConfig signingConfigs.release
            }
        }
    }

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 34
        versionName "0.57"
        versionCode  57
        applicationId "itkach.aard2"
    }
    lint {
        abortOnError false
    }

}

dependencies {
    implementation 'com.google.android.material:material:1.12.0'
    implementation "androidx.webkit:webkit:1.9.0"
    implementation "androidx.core:core:1.13.1"
    implementation "androidx.appcompat:appcompat:1.7.0"
    api 'com.fasterxml.jackson.core:jackson-databind:2.4+'
    api 'com.fasterxml.jackson.core:jackson-core:2.4+'
    api 'com.fasterxml.jackson.core:jackson-annotations:2.4+'
    api 'com.github.kazy1991:FontDrawable:0.9.5'
    api project(':slobj')
    api project(':slobber')
}
