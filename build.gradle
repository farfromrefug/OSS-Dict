plugins {
    id 'com.android.application' version '8.4.2'
    id 'org.jetbrains.kotlin.android' version '1.8.0'
}

repositories {
    mavenCentral()
    google()
    maven {
        url "https://jitpack.io"
    }
}

android {
    namespace "com.akylas.aard2"

    packagingOptions {
        project("slobj").file("exclude.txt").eachLine {
            line ->
                println line
                if (line && line.trim() && !line.startsWith("#")) {
                    exclude line.trim()
                }
        }
        resources {
            excludes += ['META-INF/LICENSE', 'META-INF/NOTICE']
        }
    }


    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }
    }

    if (System.getenv("KEYSTORE") != null) {
        signingConfigs {
            release {
                storeFile file(System.getenv("KEYSTORE"))
                storePassword System.getenv("STORE_PASSWORD")
                keyPassword System.getenv("KEY_PASSWORD")
                if (keyPassword == null) {
                    keyPassword = storePassword
                }
                keyAlias "aard2-android"
            }
        }
    }

    buildTypes {
        if (System.getenv("KEYSTORE") != null) {
            release {
                signingConfig signingConfigs.release
            }
        }
    }
    compileSdkVersion 35
    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 35
        compileSdk 35
        versionName "1.0.2"
        versionCode  60
        applicationId "com.akylas.aard2"
    }
    lint {
        abortOnError false
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

}

dependencies {
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.8.1'
    implementation 'com.google.android.material:material:1.12.0'
    implementation "androidx.webkit:webkit:1.12.1"
    implementation "androidx.appcompat:appcompat:1.7.0"
    implementation "androidx.viewpager2:viewpager2:1.1.0"
    implementation "androidx.navigation:navigation-ui-ktx:2.8.4"
    implementation "androidx.navigation:navigation-fragment-ktx:2.8.4"
    implementation "androidx.navigation:navigation-runtime-ktx:2.8.4"
    api 'com.fasterxml.jackson.core:jackson-databind:2.11.1'
    api 'com.fasterxml.jackson.core:jackson-core:2.11.1'
    api 'com.fasterxml.jackson.core:jackson-annotations:2.11.1'
    api 'com.github.kazy1991:FontDrawable:0.9.5'
    api project(':slobj')
    api project(':slobber')
    implementation 'androidx.core:core-ktx:1.15.0'
}
