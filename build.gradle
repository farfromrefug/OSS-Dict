plugins {
    id 'com.android.application' version '8.7.3'
}

repositories {
    mavenCentral()
    google()
    maven {
        url "https://jitpack.io"
    }
}

android {
    compileSdk 35

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    defaultConfig {
        namespace "itkach.aard2"
        applicationId "com.akylas.aard2"
        minSdk 21
        targetSdk 35
        versionCode 63
        versionName "1.0.5"
    }


    packagingOptions {
        exclude 'META-INF/*.version'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
        project("slobj").file("exclude.txt").eachLine {
            line ->
                println line
                if (line && line.trim() && !line.startsWith("#")) {
                    exclude line.trim()
                }
        }
    }



    lintOptions {
        abortOnError false
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }
    }

    if (System.getenv("KEYSTORE") != null) {
        signingConfigs {
            release {
                storeFile file(System.getenv("KEYSTORE"))
                storePassword System.getenv("STORE_PASSWORD")
                keyPassword System.getenv("KEY_PASSWORD")
                if (keyPassword == null) {
                    keyPassword = storePassword
                }
                keyAlias "aard2-android"
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            if (System.getenv("KEYSTORE") != null) {
                signingConfig signingConfigs.release
            }
        }
    }
    buildFeatures {
        buildConfig true
    }
    dependenciesInfo {
        // Disables dependency metadata when building APKs.
        includeInApk = false
        // Disables dependency metadata when building Android App Bundles.
        includeInBundle = false
    }
}

dependencies {
    api project(':slobj')

    implementation "com.google.android.material:material:1.12.0"
    implementation "androidx.appcompat:appcompat:1.7.0"

    implementation "androidx.viewpager2:viewpager2:1.1.0"

    // Fix duplicate classes issue in material
    configurations {
        configureEach {
            exclude group: 'androidx.lifecycle', module: 'lifecycle-viewmodel-ktx'
        }
    }
    implementation "androidx.webkit:webkit:1.12.1"

    api 'com.fasterxml.jackson.core:jackson-databind:2.11.1'
    api 'com.fasterxml.jackson.core:jackson-core:2.11.1'
    api 'com.fasterxml.jackson.core:jackson-annotations:2.11.1'
}
